#!/usr/bin/env just --justfile

PROJECT_NAME := 'rndhpc_prj_2024_rk6_75b_nikitinvl_kp'
OUTDIR := replace(justfile_directory() / 'out', ' ', '\ ')
SRCDIR := replace(justfile_directory() / 'src/', ' ', '\ ')
DOCDIR := replace(justfile_directory() / 'doc/', ' ', '\ ')

export BIBINPUTS := replace(SRCDIR, '\ ', ' ')
export BSTINPUTS := replace(SRCDIR, '\ ', ' ')

default:
    just --choose

clean_fig:
    rm -rf {{SRCDIR / 'figures' / '*'}}

clean:
    rm -rf {{OUTDIR}}

[private]
mk_folder:
    mkdir -p {{OUTDIR}}
    mkdir -p {{OUTDIR}}/chapters

[private]
build_pdf:
    cd {{ SRCDIR }} && latexmk -quiet -outdir={{ OUTDIR }} -pdf {{ PROJECT_NAME }} --shell-escape

[private]
build_gls:
    cd {{ OUTDIR }} && makeglossaries-lite {{ PROJECT_NAME }}

[private]
build_bib:
    cd {{ OUTDIR }} && bibtex {{ PROJECT_NAME }}

build: mk_folder build_pdf build_gls build_bib && build_pdf

rebuild: clean clean_fig compile build

dist: rebuild
    cp {{ OUTDIR }}/{{ PROJECT_NAME }}.pdf {{ DOCDIR }}

compile_mermaid file ext='.pdf':
    mmdc -i {{ SRCDIR / 'mermaid' / file }} -o {{ SRCDIR / 'figures' / without_extension(file) + ext }} -b transparent -f -C {{ SRCDIR / 'mermaid' / 'mermaid.css' }}

compile: clean_fig
    #!/usr/bin/env python3
    from glob import glob
    from subprocess import run
    from os import path
    for file in glob("{{replace(SRCDIR, '\ ', ' ')}}mermaid/*.mmd"):
        run(['just', 'compile_mermaid', path.basename(file)])
