#!/usr/bin/env just --justfile

PROJECT_NAME := 'rndhpc_prj_2024_rk6_75b_nikitinvl_vkr'
OUTDIR := replace(justfile_directory() / 'out', ' ', '\ ')
SRCDIR := replace(justfile_directory() / 'src/', ' ', '\ ')
DOCDIR := replace(justfile_directory() / 'doc/', ' ', '\ ')

export BIBINPUTS := replace(SRCDIR, '\ ', ' ')
export BSTINPUTS := replace(SRCDIR, '\ ', ' ')

default:
    just --choose

clean:
    rm -rf {{OUTDIR}}

[private]
mk_folder:
    mkdir -p {{OUTDIR}}
    mkdir -p {{OUTDIR}}/chapters
    mkdir -p {{ SRCDIR }}/figures/.generated

[private]
build_pdf: mk_folder
    cd {{ SRCDIR }} && latexmk -quiet -outdir={{ OUTDIR }} -pdf {{ PROJECT_NAME }} --shell-escape

[private]
build_gls:
    cd {{ OUTDIR }} && makeglossaries-lite {{ PROJECT_NAME }}

[private]
build_bib:
    cd {{ OUTDIR }} && bibtex {{ PROJECT_NAME }}

build: build_pdf build_gls build_bib && build_pdf

rebuild: clean compile build

watch: clean compile
	watchexec -d 2000 -w {{ SRCDIR }} -r  -- just build

dist: rebuild
    cp {{ OUTDIR }}/{{ PROJECT_NAME }}.pdf {{ DOCDIR }}

compile_mermaid file ext='.pdf': mk_folder
    mmdc                                                                           \
    	-i {{ SRCDIR / 'mermaid' / file }}                                         \
    	-o {{ SRCDIR / 'figures' / '.generated' / without_extension(file) + ext }} \
    	-b transparent                                                             \
    	-f                                                                         \
    	-C {{ SRCDIR / 'mermaid' / 'mermaid.css' }}                                \
    	-t neutral

compile_inkscape file: mk_folder
	inkscape                                                                                    \
		-D                                                                                      \
		-d 300                                                                                  \
		-z                                                                                      \
		--file={{ SRCDIR / 'inkscape' / file }}                                                 \
		--export-pdf={{ SRCDIR / 'figures' / '.generated' / without_extension(file) + '.pdf' }} \
		--export-latex

compile:
  #!/usr/bin/env sh
  for f in {{ SRCDIR }}mermaid/*.mmd; do 
    just compile_mermaid `basename "$f"`
  done
  for f in {{ SRCDIR }}inkscape/*.svg; do 
    just compile_inkscape `basename "$f"`
  done
