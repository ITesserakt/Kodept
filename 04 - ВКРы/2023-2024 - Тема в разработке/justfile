#!/usr/bin/env just --justfile

PROJECT_NAME := 'rndhpc_prj_2024_rk6_75b_nikitinvl_vkr'
OUTDIR := replace(justfile_directory() / 'out', ' ', '\ ')
SRCDIR := replace(justfile_directory() / 'src/', ' ', '\ ')
DOCDIR := replace(justfile_directory() / 'doc/', ' ', '\ ')

export BIBINPUTS := replace(SRCDIR, '\ ', ' ')
export BSTINPUTS := replace(SRCDIR, '\ ', ' ')

default:
    just --choose

clean:
    rm -rf {{OUTDIR}}

[private]
mk_folder:
    @mkdir -p {{OUTDIR}}
    @mkdir -p {{OUTDIR}}/chapters
    @mkdir -p {{ SRCDIR }}/figures/.generated

[private]
build_pdf: mk_folder
    cd {{ SRCDIR }} && xelatex -output-directory={{ OUTDIR }} -interaction=batchmode -halt-on-error {{ PROJECT_NAME }}

[private]
build_gls:
    cd {{ OUTDIR }} && makeglossaries-lite -q {{ PROJECT_NAME }}

[private]
build_bib:
    cd {{ OUTDIR }} && bibtex {{ PROJECT_NAME }}

build: build_pdf build_gls build_bib build_pdf

rebuild: clean compile build

dist: rebuild
    cp {{ OUTDIR }}/{{ PROJECT_NAME }}.pdf {{ DOCDIR }}

compile_inkscape file: mk_folder
	@inkscape                                                                                    \
		-D                                                                                      \
		-d 300                                                                                  \
		-z                                                                                      \
		--file={{ SRCDIR / 'inkscape' / file }}                                                 \
		--export-pdf={{ SRCDIR / 'figures' / '.generated' / without_extension(file) + '.pdf' }} \
		--export-latex

compile_uml theme: mk_folder
	@plantuml                                     \
		-tlatex:nopreamble                        \
		-theme {{ theme }}                        \
		-config {{ 'thirdparty' / 'config.cfg' }} \
		{{ 'src' / 'plantuml' / '*.puml' }}

compile: (compile_uml "_none_")
  #!/usr/bin/env sh
  for f in {{ SRCDIR }}inkscape/*.svg; do 
    just compile_inkscape `basename "$f"`
  done
